/*
 *
 An ArticleArea can be either a container for other areas, or a container for a single article.
 *
 */

context ArticleArea {

        constraint areaOrArticle{

                check : self.article.isDefined() implies self.subArea.isEmpty()
                message: "ArticleArea " + self.ID + " must contain either an article or an area.
                Contains \"" + self.article.articleTitle + "\" and " + self.getSubAreasID()
        }
}

// Same for AdArea

context AdArea {

        constraint areaOrAd{

                check : self.ad.isDefined() implies self.subArea.isEmpty()
                message: "AdvertisingArea " + self.ID + " must contain either an article or an area. Contains \"" + self.ad.adTitle + "\" and " + self.getSubAreasID()
        }
}

// Helper method

operation Area getSubAreasID() : String {
        var ids = "";
        for (id : Integer in self.subArea.ID){
                ids = ids + "Area " + id + ", ";
        }
        return ids;
}


/*
 *
 An area cannot exceed the borders of its parent area
 *
 */

context Area {
		
		constraint insideParentAreaLeft{
                guard : not self.isTypeOf(Page)
                check : self.xTopLeft >= self.getParentTopLeft("x")
                message : "(Left) ArticleArea " + self.ID + " (xTopLeft=" + self.xTopLeft + 
                ") exceeds parent area (xTopLeft=" + self.getParentTopLeft("x") + ") "
        }
        
        constraint insideParentAreaTop{
                guard : not self.isTypeOf(Page)
                check : self.yTopLeft <= self.getParentTopLeft("y")
                message : "(Top) ArticleArea " + self.ID + " (yTopLeft=" + self.yTopLeft + 
                ") exceeds parent area (yTopLeft=" + self.getParentTopLeft("y") + ") "
        }

        constraint insideParentBottom{
                guard : not self.isTypeOf(Page)
                check : self.yBottomRight >= self.getParentBottomRight("y")
                message : "(Bottom) ArticleArea " + self.ID + " (yTopLeft=" + self.yTopLeft + 
                ") exceeds parent area (yTopLeft=" + self.getParentBottomRight("y") + ") "
        }
        
        constraint insideParentAreaRight{
                guard : not self.isTypeOf(Page)
                check : self.xBottomRight <= self.getParentBottomRight("x")
                message : "(Right) ArticleArea " + self.ID + " (xTopLeft=" + self.xTopLeft + 
                ") exceeds parent area (xTopLeft=" + self.getParentBottomRight("x") + ") "
        }

}

// Helper methods

operation Area getParent() : Area {
		// If an area is a direct child of a page, the parent relation has a different name (page)
		if (self.parentArea.isUndefined()) {
			return self.page;
		}
		else { return self.parentArea; }
}

operation Area getParentTopLeft(c : String) : Real{
		if ( c = "x" ) { return self.getParent().xTopLeft; }
		else { if ( c = "y" ) { return self.getParent().yTopLeft; } }
}

operation Area getParentBottomRight(c : String) : Real{
		if ( c = "x" ) { return self.getParent().xBottomRight; }
		else { if ( c = "y" ) { return self.getParent().yBottomRight;} }
}

/*
 *
 TopLeft must be top/left of BottomRight
 *
 */
 
context Area {
	constraint noSwap {
		check : self.xTopLeft < self.xBottomRight and self.yTopLeft > self.yBottomRight
		message : "Area " + self.ID + " is invalid. TopLeft and BottomRight are swapped."
	}

} 

/* 
 *
 Areas cannot overlap
 *
 */
 
 context Area {
 	guard : not self.isTypeOf(Page)
 	constraint noOverlap {
 		check : self.overlapping() = ""
 		message : self.overlapping()
 	}
 }
 
operation Area getSiblings() : Set {
	if ( self.parentArea.isUndefined() ) { 
		return self.page.area; 
	}
	else { 
		return self.parentArea.subArea; 
	}
}

operation Area overlapping() : String {
	for ( sibling : Area in self.getSiblings().excluding(self) ) {
		if ( 
		( sibling.xTopLeft < self.xBottomRight and not ( sibling.xBottomRight < self.xTopLeft ) ) and
		( sibling.yTopLeft > self.yBottomRight and not ( sibling.yBottomRight > self.yTopLeft ) ) 
		) { 
			return "Area " + self.ID + " overlaps with Area " + sibling.ID; 
		}
	}
	return "";
}

/*
 *
 All article in a page must have the same topic as the page
 *
 */
 
 context Page {
 
 	constraint sameTopic {
 
 		check : self.getAllChildrenTopicNames().forAll( s : String | s = self.topic.name )
 		message : "All articles and advertisements must have the same topic as their parent page " + self.pageNumber + ". Page topic is " + self.topic.name + " and children topics are " + self.getAllChildrenTopicNames()
 		
 	}
 }
 
 operation Page getAllAreas() : Collection {
 	return self.area.includingAll(self.area.closure(area : Area | area.subArea ));
 }
 
 operation Page getAllChildrenTopicNames() : Collection {
 	return self.getAllAreas().collect( a : ArticleArea | a.article).includingAll(self.getAllAreas().collect( a : AdArea | a.ad)).select( o : Any | o.isDefined()).metadata.topic.name;
 }
 
/*
 *
 Page numbers must be consecutive and different
 *
 */
context Newspaper {
	
	constraint pageNumbers{
		check : self.page.notEmpty() implies ( self.consecutive() and self.different() )
		message : "Page numbers must be consecutive: " + self.consecutive() + " and different: " + self.different() + ". Pages: " + self.page.pageNumber
	}
}

operation Newspaper consecutive() : Boolean {
	var pages = self.page.excluding( self.getLastPage() );
	return pages.forAll( p : Page | self.page.pageNumber.includes(p.pageNumber+1));
}

operation Newspaper getLastPage() : Page {
	return self.page.select( p : Page | p.pageNumber = self.page.pageNumber.max() ).first();
}

operation Newspaper different() : Boolean {
	return self.page.forAll( p1 : Page | self.page.excluding(p1).forAll( p2 : Page | p2.pageNumber <> p1.pageNumber ) );
}

/*
 *
 Pages must have the same size and absolute placement
 *
 */
 
 context Newspaper {
 	
 	constraint pagesSameSize{
 		check : self.page.xTopLeft.forAll( n : Real | n=0 ) 
 			and self.page.yBottomRight.forAll( n : Real | n=0 )
 			and self.page.yTopLeft.forAll( n1 : Real | self.page.yTopLeft.forAll( n2 : Real | n2=n1 ) )
 			and self.page.xBottomRight.forAll( n1 : Real | self.page.xBottomRight.forAll( n2 : Real | n2=n1 ) )
 		message: "All pages must have the same size and absolute placement"
 	}
 }